# Maintainer: Carneiro <gabriel.chaves.carneiro@gmail.com>
pkgname=st
pkgver=0.8.4
pkgrel=1
pkgdesc="A simple virtual terminal emulator for X"
url=http://st.suckless.org
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=(libxft)
makedepends=('git' 'make')
#optdepends=('polybar-dwm-module: polybar with dwm module' 'st' 'dmenu')
source=(
		https://dl.suckless.org/st/st-$pkgver.tar.gz
		config.h
		st.desktop
		)
_patches=(
		bold-is-not-bright.diff
		focus.diff
		font2.diff
		scrollback.diff
		scrollback-mouse.diff
		scrollback-mouse-altscreen.diff
		xresources.diff
		)
source=(${source[@]} ${_patches[@]})
sha256sums=(
'd42d3ceceb4d6a65e32e90a5336e3d446db612c3fbd9ebc1780bc6c9a03346a6'
'SKIP'
'17924fbac300031b0f8b0bbbf85d18c4637a78e7260350714d4062d64783d46d'
'329169acac7ceaf901995d6e0897913089b799d8cd150c7f04c902f4a5b8eab2'
'b43c113f0e8124f88803443d30a3c6e3e3e0f8c7d792f98fd1a0c4bbc383060f'
'7279c787dba23d72a8a0a4613c0917e03d0087f0254f56a530cd9c521857d20b'
'1e41fe17a5ef5a8194eea07422b49d815e2c2bb4d58d84771f793be423005310'
'319458d980195d18fa0f81a6898d58f8d046c5ff982ab872d741f54bb60e267d'
'cb87eb654985da46ff63663407184402393ad3d3013c8795570552fe56a15b9d'
'5be9b40d2b51761685f6503e92028a7858cc6571a8867b88612fce8a70514d5b'
)

build() {
	cd $pkgname-$pkgver
	for p in "${_patches[@]}"; do
		echo "=> $p"
		patch -p1 < ../$p || return 1
	done
	cp -R $srcdir/config.h config.def.h
	make
}

package() {
	cd $pkgname-$pkgver
	make PREFIX=/usr DESTDIR=$pkgdir install
	install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
	install -m644 -D $srcdir/st.desktop $pkgdir/usr/share/applications/st.desktop
}
